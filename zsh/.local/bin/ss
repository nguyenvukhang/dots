#!/usr/bin/env zsh

# start server script: ss

SERVER_SESSION="server"
FZF_OPTS=(--height=7 +m --no-mouse --reverse --no-info --prompt='  ' --color='pointer:green,header:white')

typeset -A START_DIRLIST=(
  [min]="$HOME/repos/minnesoda/min"
  [sun-app]="$HOME/sunnus/app/dev"
  [sun-srv]="$HOME/sunnus/cloud/functions"
)

typeset -A COMMANDS=(
  [min]="yarn dev"
  [sun-app]="yarn dev"
  [sun-srv]="yarn serve"
)

KEY=$(for i in ${(k)START_DIRLIST}; do
  echo $i
done | fzf $FZF_OPTS --header="Pick a repo to start a server from")

[[ -z $KEY ]] \
  && echo "No repo selected. Quitting." \
  && return 0

# results in c containing either $HOME or start dir
s=$START_DIRLIST[$KEY]
[ $s ] && START_DIR=$s || START_DIR=$HOME

# check for existence of SERVER_SESSION
! tmux has-session -t "$SERVER_SESSION" 2>/dev/null
session_exists=$?

# create it if it doesn't exist
create_session() { tmux new-session -d \
  -s $SERVER_SESSION -c $START_DIR -n $KEY \
  $COMMANDS[$KEY]
  echo "Started a tmux server to run [$KEY]."
}

create_window() { tmux new-window \
  -t $SERVER_SESSION: -c $START_DIR -n $KEY \
  $COMMANDS[$KEY]
  echo "Added a window running [$KEY] to servers."
}

# if session doesn't exist, create session and exit
[[ $session_exists == 0 ]] && create_session && exit 0

active_windows=()
window_exists=0
while IFS= read -r line; do
  [[ "$line" == "$KEY" ]] && window_exists=1
done < <(tmux list-windows -t $SERVER_SESSION -F "#{window_name}")

# if session exists but window doesn't exist, create window
[[ $session_exists == 1 && $window_exists == 0 ]] && create_window && exit 0
# if all already exists, echo that
echo "Requested server is already running."
